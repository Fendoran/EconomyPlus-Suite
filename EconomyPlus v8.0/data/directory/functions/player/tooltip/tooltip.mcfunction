## Displays the tooltip (item and time)
# removes the entity for being killed, since a player is looking at it
execute positioned ^ ^0.4 ^ run tag @e[type=snowball,tag=ep.trait.tooltip,tag=trait.kill,distance=...1] remove trait.kill
execute positioned ^ ^-1.25 ^ run tag @e[type=area_effect_cloud,tag=ep.trait.tooltip,tag=trait.kill,distance=...3] remove trait.kill
# Summons the item/AEC if it doesn't exist
execute positioned ^ ^0.4 ^ unless entity @e[type=snowball,tag=ep.trait.tooltip,distance=...1] run summon snowball ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.description', 'ep.spawning'], NoGravity: 1b, CustomNameVisible: 1b}
execute if score @s[tag=ep.trait.shop.use] ep.time matches 0.. positioned ^ ^-1.25 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.uses,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.timer,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.timer', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}
execute if score @s[tag=ep.trait.shop.dynamic] ep.time matches 0.. positioned ^ ^-1.25 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.uses,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.gtimer,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.gtimer', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}

# Uses for all signs
execute if entity @s[tag=ep.trait.market.sell] unless score @s ep.stored_money matches 0 positioned ^ ^-1.25 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.uses,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.out_of_stock,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.uses', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}
execute if entity @s[tag=ep.trait.market.buy] unless score @s ep.use matches 0 positioned ^ ^-1.25 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.uses,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.out_of_stock,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.uses', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}
execute if entity @s[tag=ep.trait.buyer,tag=ep.trait.shop.use] unless score @s ep.use matches 0 unless score @s ep.max_use matches 0 positioned ^ ^-1.25 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.uses,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.timer,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.out_of_stock,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.uses', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}
execute if entity @s[tag=ep.trait.seller,tag=ep.trait.shop.use] unless score @s ep.use matches 0 unless score @s ep.max_use matches 0 positioned ^ ^-1.25 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.uses,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.timer,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.out_of_stock,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.uses', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}

# Owner
execute as @p run function directory:uid/get
execute if entity @s[tag=ep.trait.market] run function directory:uid/compare
execute if entity @s[tag=ep.trait.market.buy,tag=ep.pass_uuid_check] positioned ^ ^-1.55 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.currency_earned,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.currency_earned', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}
execute if entity @s[tag=ep.trait.market.sell,tag=ep.pass_uuid_check] positioned ^ ^-1.55 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.currency_earned,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.currency_earned', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}

# Non-Owner
execute if entity @s[tag=ep.trait.market.buy] if score @s ep.use matches 0 positioned ^ ^-1.25 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.out_of_stock,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.uses,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.out_of_stock', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}
execute if entity @s[tag=ep.trait.market.sell] if score @s ep.stored_money matches 0 positioned ^ ^-1.25 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.out_of_stock,distance=...3] unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.uses,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.out_of_stock', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}
execute if entity @s[tag=ep.trait.market,tag=!ep.pass_uuid_check] positioned ^ ^-1.55 ^ unless entity @e[type=area_effect_cloud,tag=ep.trait.tooltip.owner,distance=...3] run summon area_effect_cloud ~ ~ ~ {Tags: ['ep.trait.tooltip', 'ep.trait.tooltip.owner', 'ep.spawning'], Age: 0, Duration: 20, CustomNameVisible: 1b}
tag @s remove ep.pass_uuid_check
